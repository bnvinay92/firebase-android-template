apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'hu.supercluster.paperwork'
apply plugin: 'com.getkeepsafe.dexcount'

//Apply last
apply plugin: 'me.tatarka.retrolambda'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 0
def versionBuild = 1

android {
    signingConfigs {
        releaseConfig {
            keyAlias KEYSTORE_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(KEYSTORE_FILE_PATH)
            storePassword KEYSTORE_PASSWORD
        }
    }
    dataBinding.enabled = true
    compileSdkVersion 25
    buildToolsVersion "24.0.3"
    defaultConfig {
        applicationId "com.github.homeroom.android"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            debuggable true
            applicationIdSuffix ".release"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
            shrinkResources true
            minifyEnabled true
        }
    }
}

dependencies {

    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta1'
    compile 'com.android.support.constraint:constraint-layout-solver:1.0.0-beta1'

    compile 'hu.supercluster:paperwork:1.2.7'

    compile 'com.google.firebase:firebase-auth:9.6.1'
    compile 'com.google.firebase:firebase-database:9.6.1'

    apt 'com.google.auto.value:auto-value:1.3'
    provided 'com.google.auto.value:auto-value:1.3'
    apt 'me.mattlogan.auto.value:auto-value-firebase:0.2.1'
    apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.4-rc2'

    apt 'com.google.dagger:dagger-compiler:2.7'
    compile 'com.google.dagger:dagger:2.7'
    provided 'javax.annotation:jsr250-api:1.0'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.0'
    compile 'com.jakewharton.rxrelay:rxrelay:1.2.0'
    compile 'com.artemzin.rxjava:proguard-rules:1.2.0.0'

    compile 'com.jakewharton.timber:timber:4.3.0'
    compile 'com.jonathanfinerty.once:once:1.2.1'
    compile 'com.heinrichreimersoftware:material-intro:1.6'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.10.19"
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

paperwork {
    set = [
            BUILD_TIME: buildTime(),
            GIT_SHA   : gitSha()
    ]
}

dexcount {
    format = "list"
    includeClasses = false
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = false
    verbose = false
    maxTreeDepth = Integer.MAX_VALUE
    teamCityIntegration = false
    enableForInstantRun = false
    teamCitySlug = null
}

apply plugin: 'com.google.gms.google-services'